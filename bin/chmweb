#!/usr/bin/perl
# App::ChmWeb - Generate browsable web pages from CHM files
# Copyright (C) 2022 Daniel Collins <solemnwarning@solemnwarning.net>
#
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License version 2 as published by
# the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along with
# this program; if not, write to the Free Software Foundation, Inc., 51
# Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

use strict;
use warnings;

use File::Basename;

use FindBin;
use lib ("$FindBin::Bin/../lib/");

use App::ChmWeb::HHCParser;
use App::ChmWeb::PageFixer;
use App::ChmWeb::Util;
use App::ChmWeb::WorkerPool;

main() unless caller;

sub main
{
	my $command = shift(@ARGV) // "";
	
	if($command eq "single-chm" && (scalar @ARGV) == 2)
	{
		my ($chm_file, $output_dir) = @ARGV;
		
		$output_dir .= "/" if($output_dir !~ m/\/$/);
		
		singlechm($chm_file, $output_dir);
	}
	elsif($command eq "multi-chm" && (scalar @ARGV) >= 2)
	{
		my $output_dir = pop(@ARGV);
		my @chm_files = @ARGV;
		
		$output_dir .= "/" if($output_dir !~ m/\/$/);
		
		multichm(\@chm_files, $output_dir);
	}
	else{
		die "Usage: $0 single-chm <input.chm> <output directory>\n"
		   ."       $0 multi-chm <input1.chm> <input2.chm> ... <output directory>\n";
	}
}

sub singlechm
{
	my ($chm_file, $output_dir) = @_;
	
	my $chm_basename = basename($chm_file);
	my $chm_basename_noext = ($chm_basename =~ s/\.chm$//ir);
	
	system("mkdir", "-p", $output_dir) and die;
	system("7z", "x", $chm_file, "-o${output_dir}", "-aoa") and die;
	
	my $hhc_name = App::ChmWeb::Util::find_hhc_in($output_dir);
	my $hhc = App::ChmWeb::HHCParser::parse_hhc_file("${output_dir}/${hhc_name}");
	
	visit_hhc_nodes($hhc->{toc}, sub
	{
		my ($node) = @_;
		
		$node->{chm_basename} = $chm_basename;
	});
	
	{
		my $pool = App::ChmWeb::WorkerPool->new(\&process_hhc_level_do_work);
		
		process_hhc_level($hhc->{toc}, [], $hhc->{toc}, $output_dir, $pool);
		
		# Wait for outstanding tasks
		$pool->drain();
	}
}

sub multichm
{
	my ($chm_files, $output_dir) = @_;
	
	my @merged_toc = ();
	my %chms = ();
	
	foreach my $chm_file(@$chm_files)
	{
		my $chm_basename = basename($chm_file);
		my $chm_basename_noext = ($chm_basename =~ s/\.chm$//ir);
		
		if($chms{$chm_basename_noext})
		{
			die "Multiple input files with same name ($chm_basename) on command line\n";
		}
		
		$chms{$chm_basename_noext} = 1;
		
		my $chm_target_dir = "$output_dir/$chm_basename_noext";
		
		system("mkdir", "-p", $chm_target_dir) and die;
		system("7z", "x", $chm_file, "-o${chm_target_dir}", "-aoa") and die;
		
		my $hhc_name = App::ChmWeb::Util::find_hhc_in($chm_target_dir);
		my $hhc = App::ChmWeb::HHCParser::parse_hhc_file("${chm_target_dir}/${hhc_name}");
		
		visit_hhc_nodes($hhc->{toc}, sub
		{
			my ($node) = @_;
			
			$node->{Local} = $chm_basename_noext."/".$node->{Local} if(defined $node->{Local});
			$node->{chm_basename} = $chm_basename;
		});
		
		push(@merged_toc, @{ $hhc->{toc} });
	}
	
	{
		my $pool = App::ChmWeb::WorkerPool->new(\&process_hhc_level_do_work);
		
		process_hhc_level(\@merged_toc, [], \@merged_toc, $output_dir, $pool);
		
		# Wait for outstanding tasks
		$pool->drain();
	}
}

sub visit_hhc_nodes
{
	my ($nodes, $callback) = @_;
	
	foreach my $node(@$nodes)
	{
		$callback->($node);
		visit_hhc_nodes($node->{children}, $callback) if(defined $node->{children});
	}
}

sub process_hhc_level
{
	my ($toc_root, $toc_path, $nodes, $output_dir, $pool) = @_;
	
	my $indent_s = " " x ((scalar @$toc_path) * 4);
	
	for(my $i = 0; $i < (scalar @$nodes); ++$i)
	{
		my $node = $nodes->[$i];
		
		print $node->{chm_basename}, ": ", $indent_s, $node->{Name}, (defined($node->{Local}) ? " (".$node->{Local}.")" : ""), "...\n";
		
		if(defined $node->{Local})
		{
			my $orig_path = $node->{Local};
			$orig_path =~ s/#.*$//;
			
			$orig_path = App::ChmWeb::Util::resolve_mixed_case_path($orig_path, $output_dir);
			
			if(defined $orig_path)
			{
				$pool->post([ $toc_root, [ @$toc_path, $i ], $node->{Name}, $output_dir, $orig_path ], sub {});
				# process_hhc_level_do_work($toc_root, [ @$toc_path, $i ], $node->{Name}, $output_dir, $orig_path );
			}
			else{
				warn "WARNING: Missing file referenced in hhc: ".$node->{Local}."\n";
			}
		}
		
		process_hhc_level($toc_root, [ @$toc_path, $i ], $node->{children}, $output_dir, $pool) if defined $node->{children};
	}
}

sub process_hhc_level_do_work
{
	my ($toc_root, $toc_path, $page_title, $output_dir, $page_path) = @_;
	
	my $content_path = ($page_path =~ s/\.(\w+)$/.content.$1/r);
	
	write_content_page($page_path, $content_path, $output_dir);
	
	write_wrapper_page($toc_root, $toc_path, $page_title, $output_dir, $page_path, $content_path);
}

sub write_content_page
{
	my ($orig_path, $content_path, $output_dir) = @_;
	
	my $pf = App::ChmWeb::PageFixer->new();
	$pf->load_content($orig_path, $output_dir);
	
	$pf->fix_image_paths();
	$pf->fix_absolute_links();
	$pf->set_default_link_target("_parent");
	
	$pf->write_content("${output_dir}${content_path}");
}

sub write_wrapper_page
{
	my ($toc_root, $toc_path, $page_title, $output_dir, $wrapper_path, $content_path) = @_;
	
	my $toc = build_wrapper_toc($toc_root, $toc_path, $output_dir, $wrapper_path);
	
	my $content_name = App::ChmWeb::Util::resolve_link($output_dir, $wrapper_path, "/".$content_path);
	# my $content_name = basename($content_path);
	
	open(my $fh, ">", "${output_dir}${wrapper_path}") or die "${output_dir}${wrapper_path}: $!";
	print {$fh} <<EOF;
<html>
<head>
<title>$page_title</title>
</head>

<body>
<div style="position: fixed; top: 0px; left: 0px; height: 100%; width: 200px; overflow: scroll;">
$toc
</div>
<iframe src="$content_name" style="position: fixed; top: 0px; width: calc(100% - 200px); left: 200px; height:100%;"></iframe>
</body>
</html>
EOF
}

sub build_wrapper_toc
{
	my ($toc_nodes, $toc_path, $output_dir, $wrapper_path) = @_;
	
	my ($toc_this, @toc_next) = @$toc_path;
	
	my $r = "<ul style=\"padding-left:1em;\">\n";
	
	for(my $i = 0; $i < @$toc_nodes; ++$i)
	{
		my $node = $toc_nodes->[$i];
		
		my $aa = "";
		my $ab = "";
		
		if(defined $node->{Local})
		{
			my $href = App::ChmWeb::Util::resolve_link($output_dir, $wrapper_path, "/".$node->{Local});
			$aa = "<a href=\"$href\">";
			$ab = "</a>";
		}
		
		my $ba = "";
		my $bb = "";
		
		if(defined($toc_this) && $i == $toc_this)
		{
			$ba = "<b>";
			$bb = "</b>";
		}
		
		$r .= "<li>${ba}${aa}".$toc_nodes->[$i]->{Name}."${ab}${bb}</b>\n";
		
		if(
			# Node has children...
			defined($node->{children})
				# ...the current page is at/under it...
				&& ((defined($toc_this) && $i == $toc_this)
				# ...or it doesn't have a link (wouldn't be expandable by itself)
				|| !defined($node->{Local})))
		{
			$r .= build_wrapper_toc($node->{children}, \@toc_next, $output_dir, $wrapper_path);
		}
		
		$r .= "</li>\n";
	}
	
	$r .= "</ul>\n";
	
	return $r;
}
