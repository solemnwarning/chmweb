#!/usr/bin/perl
# App::ChmWeb - Generate browsable web pages from CHM files
# Copyright (C) 2022 Daniel Collins <solemnwarning@solemnwarning.net>
#
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License version 2 as published by
# the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along with
# this program; if not, write to the Free Software Foundation, Inc., 51
# Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

use strict;
use warnings;

use File::Basename;

use FindBin;
use lib ("$FindBin::Bin/../lib/");

use App::ChmWeb::ContentPageWriter;
use App::ChmWeb::HHCParser;
use App::ChmWeb::TreeScanner;
use App::ChmWeb::Util;
use App::ChmWeb::WorkerPool;

main() unless caller;

sub main
{
	my $command = shift(@ARGV) // "";
	
	if($command eq "single-chm" && (scalar @ARGV) == 2)
	{
		my ($chm_file, $output_dir) = @ARGV;
		
		$output_dir .= "/" if($output_dir !~ m/\/$/);
		
		singlechm($chm_file, $output_dir);
	}
	elsif($command eq "multi-chm" && (scalar @ARGV) >= 2)
	{
		my $output_dir = pop(@ARGV);
		my @chm_files = @ARGV;
		
		$output_dir .= "/" if($output_dir !~ m/\/$/);
		
		multichm(\@chm_files, $output_dir);
	}
	else{
		die "Usage: $0 single-chm <input.chm> <output directory>\n"
		   ."       $0 multi-chm <input1.chm> <input2.chm> ... <output directory>\n";
	}
}

sub singlechm
{
	my ($chm_file, $output_dir) = @_;
	
	my $chm_basename = basename($chm_file);
	my $chm_basename_noext = ($chm_basename =~ s/\.chm$//ir);
	
	system("mkdir", "-p", $output_dir) and die;
	system("7z", "x", $chm_file, "-o${output_dir}", "-aoa") and die;
	
	my $hhc_name = App::ChmWeb::Util::find_hhc_in($output_dir);
	my $hhc = App::ChmWeb::HHCParser::parse_hhc_file("${output_dir}/${hhc_name}");
	
	visit_hhc_nodes($hhc->{toc}, sub
	{
		my ($node) = @_;
		
		$node->{chm_basename} = $chm_basename;
	});
	
	{
		my $pool = App::ChmWeb::WorkerPool->new(sub { process_hhc_level_do_work($hhc->{toc}, @_) });
		
		process_hhc_level($hhc->{toc}, [], $hhc->{toc}, $output_dir, $pool);
		
		# Wait for outstanding tasks
		$pool->drain();
	}
}

sub multichm
{
	my ($chm_files, $output_dir) = @_;
	
	my @merged_toc = ();
	my %chms = ();
	
	foreach my $chm_file(@$chm_files)
	{
		my $chm_basename = basename($chm_file);
		my $chm_basename_noext = ($chm_basename =~ s/\.chm$//ir);
		
		if($chms{$chm_basename_noext})
		{
			die "Multiple input files with same name ($chm_basename) on command line\n";
		}
		
		$chms{$chm_basename_noext} = 1;
		
		my $chm_target_dir = "$output_dir/$chm_basename_noext";
		
		system("mkdir", "-p", $chm_target_dir) and die;
		system("7z", "x", $chm_file, "-o${chm_target_dir}", "-aoa") and die;
	}
	
	STDERR->autoflush(1);
	
	my $verbosity = 1;
	my $data = App::ChmWeb::TreeScanner->scan_tree($output_dir, [ map { "$_/" } keys(%chms) ], $verbosity);
	
	print STDERR "Resolving links...\n";
	
	my %link_map = map { $_ => App::ChmWeb::Util::resolve_mixed_case_path($_, $output_dir) }
		(@{ $data->{page_links} }, @{ $data->{asset_links} });
	
	print STDERR "Generating pages...";
	
	my $page_generator = App::ChmWeb::WorkerPool->new(sub
	{
		my ($page_path) = @_;
		
		my $toc_path = find_toc_path_for_page($data->{toc}, $page_path) // [];
		
		my $content_path = ($page_path =~ s/\.(\w+)$/.content.$1/r);
		
		my ($chm_root) = grep { $page_path =~ m/^\Q$_\E/ } map { "$_/" } keys(%chms);
		
		write_content_page($page_path, $content_path, $output_dir, \%link_map, $chm_root);
		
		write_wrapper_page($data->{toc}, $toc_path, ($data->{page_titles}->{$page_path} // ""), $output_dir, $page_path, $content_path, \%link_map);
		
		return;
	});
	
	my $pages_generated = 0;
	my $pages_total = (scalar @{ $data->{page_paths} });
	
	my $x = 0;
	foreach my $page_path(@{ $data->{page_paths} })
	{
		$page_generator->post([ $page_path ], sub
		{
			++$pages_generated;
			
			if(($pages_generated % 100) == 0 || $pages_generated == $pages_total)
			{
				print STDERR "\rGenerating pages... ($pages_generated / $pages_total)";
			}
		});
		
		if(++$x == 1000)
		{
			#last;
		}
	}
	
	$page_generator->drain();
	$page_generator = undef;
	
	print STDERR "\n";
	
	generate_stub_pages($data->{toc}, $output_dir, \%link_map);
}

sub find_toc_path_for_page
{
	my ($toc, $page_path) = @_;
	
	my $process_toc = sub
	{
		my ($process_toc, $toc_nodes, $toc_path) = @_;
		
		for(my $i = 0; $i < (scalar @$toc_nodes); ++$i)
		{
			my $toc_node = $toc_nodes->[$i];
			
			if(defined($toc_node->{page_path}) && $toc_node->{page_path} eq $page_path)
			{
				return [ @$toc_path, $i ];
			}
			
			if(defined($toc_node->{children}))
			{
				my $r = $process_toc->($process_toc, $toc_node->{children}, [ @$toc_path, $i ]);
				return $r if(defined $r);
			}
		}
		
		return undef;
	};
	
	return $process_toc->($process_toc, $toc, []);
}

sub generate_stub_pages
{
	my ($toc_root, $output_dir, $link_map) = @_;
	
	print STDERR "Generating stub pages...";
	
	my $stub_page_generator = App::ChmWeb::WorkerPool->new(sub
	{
		my ($toc_path, $title) = @_;
		
		my $stub_path = join("_", "s", @$toc_path).".htm";
		
		write_wrapper_page($toc_root, $toc_path, $title, $output_dir, $stub_path, undef, $link_map);
	});
	
	my $walk_toc_tree = sub
	{
		my ($walk_toc_tree, $toc_nodes, $toc_path) = @_;
		
		for(my $i = 0; $i < (scalar @$toc_nodes); ++$i)
		{
			my $toc_node = $toc_nodes->[$i];
			
			unless(defined $toc_node->{page_path})
			{
				$stub_page_generator->post([ [ @$toc_path, $i ], $toc_node->{name} ], sub {});
			}
			
			if(defined $toc_node->{children})
			{
				$walk_toc_tree->($walk_toc_tree, $toc_node->{children}, [ @$toc_path, $i ]);
			}
		}
	};
	
	$walk_toc_tree->($walk_toc_tree, $toc_root, []);
	
	$stub_page_generator->drain();
	$stub_page_generator = undef;
	
	print STDERR "\n";
}

sub generate_stub_page_path
{
	my ($toc_path) = @_;
	return join("_", "s", @$toc_path).".htm"
}

sub visit_hhc_nodes
{
	my ($nodes, $callback) = @_;
	
	foreach my $node(@$nodes)
	{
		$callback->($node);
		visit_hhc_nodes($node->{children}, $callback) if(defined $node->{children});
	}
}

sub process_hhc_level
{
	my ($toc_root, $toc_path, $nodes, $output_dir, $pool) = @_;
	
	#my $indent_s = " " x ((scalar @$toc_path) * 4);
	
	for(my $i = 0; $i < (scalar @$nodes); ++$i)
	{
		my $node = $nodes->[$i];
		
		#print $node->{chm_basename}, ": ", $indent_s, $node->{Name}, (defined($node->{Local}) ? " (".$node->{Local}.")" : ""), "...\n";
		
		if(defined $node->{Local})
		{
			my $orig_path = $node->{Local};
			$orig_path =~ s/#.*$//;
			
			$orig_path = App::ChmWeb::Util::resolve_mixed_case_path($orig_path, $output_dir);
			
			if(defined $orig_path)
			{
				$pool->post([ [ @$toc_path, $i ], $node->{Name}, $output_dir, $orig_path ], sub {});
				# process_hhc_level_do_work($toc_root, [ @$toc_path, $i ], $node->{Name}, $output_dir, $orig_path );
			}
			else{
				warn "WARNING: Missing file referenced in hhc: ".$node->{Local}."\n";
			}
		}
		
		process_hhc_level($toc_root, [ @$toc_path, $i ], $node->{children}, $output_dir, $pool) if defined $node->{children};
	}
}

sub process_hhc_level_do_work
{
	my ($toc_root, $toc_path, $page_title, $output_dir, $page_path) = @_;
	
	warn "process_hhc_level_do_work: $page_path\n";
	
	my $content_path = ($page_path =~ s/\.(\w+)$/.content.$1/r);
	
	write_content_page($page_path, $content_path, $output_dir);
	
	write_wrapper_page($toc_root, $toc_path, $page_title, $output_dir, $page_path, $content_path);
}

sub write_content_page
{
	my ($orig_path, $content_path, $output_dir, $link_map, $chm_root) = @_;
	
	eval {
		my $pf = App::ChmWeb::ContentPageWriter->new($link_map, $chm_root);
		$pf->load_content($orig_path, $output_dir);
		
		$pf->modify_content();
		
		$pf->write_content("${output_dir}${content_path}");
	};
	
	if($@)
	{
		warn "write_content_page: $@\n";
	}
}

sub write_wrapper_page
{
	my ($toc_root, $toc_path, $page_title, $output_dir, $wrapper_path, $content_path, $link_map) = @_;
	
	my $toc = build_wrapper_toc($toc_root, [], $toc_path, $output_dir, $wrapper_path, $link_map);
	
	open(my $fh, ">", "${output_dir}${wrapper_path}") or do { warn "write_wrapper_page: ${output_dir}${wrapper_path}: $!"; return; };
	print {$fh} <<EOF;
<html>
<head>
<title>$page_title</title>
</head>

<body>
<div style="position: fixed; top: 0px; left: 0px; height: 100%; width: 200px; overflow: scroll;">
$toc
</div>
EOF
	
	if(defined $content_path)
	{
		my $local_content_path = App::ChmWeb::Util::root_relative_path_to_doc_relative_path($content_path, $wrapper_path);
		print {$fh} <<EOF;
<iframe src="$local_content_path" style="position: fixed; top: 0px; width: calc(100% - 200px); left: 200px; height:100%;"></iframe>
EOF
	}
	
	print {$fh} <<EOF;
</body>
</html>
EOF
}

sub build_wrapper_toc
{
	my ($toc_nodes, $toc_path, $page_toc_path, $output_dir, $wrapper_path, $link_map) = @_;
	
	my ($toc_this, @toc_next) = @$page_toc_path;
	
	my $r = "<ul style=\"padding-left:1em;\">\n";
	
	for(my $i = 0; $i < @$toc_nodes; ++$i)
	{
		my $node = $toc_nodes->[$i];
		
		my $aa = "";
		my $ab = "";
		
		if(defined($node->{page_path}))
		{
			if(defined($link_map->{ $node->{page_path} }))
			{
				my $href = App::ChmWeb::Util::root_relative_path_to_doc_relative_path($link_map->{ $node->{page_path} }, $wrapper_path);
				$aa = "<a href=\"$href\">";
				$ab = "</a>";
			}
			else{
				# die "Broken link in TOC: ".$node->{page_path};
			}
		}
		else{
			my $href = App::ChmWeb::Util::root_relative_path_to_doc_relative_path(generate_stub_page_path([ @$toc_path, $i ]), $wrapper_path);
			$aa = "<a href=\"$href\">";
			$ab = "</a>";
		}
		
		my $ba = "";
		my $bb = "";
		
		if(defined($toc_this) && $i == $toc_this)
		{
			$ba = "<b>";
			$bb = "</b>";
		}
		
		$r .= "<li>${ba}${aa}".($toc_nodes->[$i]->{name} // "???")."${ab}${bb}</b>\n";
		
		if(defined($node->{children}))
		{
			# Node has children...
			
			if(defined($toc_this) && $i == $toc_this)
			{
				# ... and the current page is at/under it
				$r .= build_wrapper_toc($node->{children}, [ @$toc_path, $i ], \@toc_next, $output_dir, $wrapper_path, $link_map);
			}
			elsif(!defined($node->{page_path}))
			{
				# ...and it doesn't have a link (wouldn't be expandable by itself)
				# $r .= build_wrapper_toc($node->{children}, [], $output_dir, $wrapper_path, $link_map);
			}
		}
		
		$r .= "</li>\n";
	}
	
	$r .= "</ul>\n";
	
	return $r;
}
